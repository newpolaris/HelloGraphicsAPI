include_directories(${ROOT_PATH}/source/lib3d)
include_directories(${ROOT_PATH}/source/libplatform)
include_directories(${ROOT_PATH}/external/glfw/deps)
include_directories(${ROOT_PATH}/external/glfw/include)
include_directories(${ROOT_PATH}/external/meshoptimizer/src)
include_directories(${ROOT_PATH}/external/objparser)
include_directories(${ROOT_PATH}/external/glad/include) 

function(buildSampleAppChild SAMPLE_NAME)
    set(SAMPLE_FOLDER ${CMAKE_CURRENT_SOURCE_DIR}/${SAMPLE_NAME})
    set(COMMON_FOLDER ${CMAKE_CURRENT_SOURCE_DIR}/common)
    message(STATUS "Generating project file for example in ${SAMPLE_FOLDER}")

    project(${SAMPLE_NAME})
    file(GLOB HEADER_LIST ${SAMPLE_NAME}/*.h ${SAMPLE_NAME}/*.hpp)
    file(GLOB SOURCE_LIST ${SAMPLE_NAME}/*.cpp ${SAMPLE_NAME}/*.mm)
    source_group(${SAMPLE_NAME} FILES ${HEADER_LIST})
    source_group(${SAMPLE_NAME} FILES ${SOURCE_LIST})

    set(SHADER_DIR ${CMAKE_CURRENT_SOURCE_DIR}/${SAMPLE_NAME})
    file(GLOB SHADER_HEADERS "${SHADER_DIR}/*.h")
    file(GLOB SHADERS "${SHADER_DIR}/*.vert" "${SHADER_DIR}/*.frag" "${SHADER_DIR}/*.comp" "${SHADER_DIR}/*.geom" "${SHADER_DIR}/*.tesc" "${SHADER_DIR}/*.tese" "${SHADER_DIR}/*.mesh")
    source_group("shaders" FILES ${SHADERS} ${SHADER_HEADERS})

    add_executable(${SAMPLE_NAME} ${HEADER_LIST} ${SOURCE_LIST} ${SHADERS} ${SHADER_HEADERS})

    target_link_libraries(${SAMPLE_NAME} PRIVATE "libsampleapp")
    target_include_directories(${SAMPLE_NAME} PRIVATE "${libsampleapp_SOURCE_DIR}")

    set_target_properties(${SAMPLE_NAME} PROPERTIES VS_DEBUGGER_WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/${SAMPLE_NAME})
    set_target_properties(${SAMPLE_NAME} PROPERTIES PROJECT_LABEL ${SAMPLE_NAME})
    set_target_properties(${SAMPLE_NAME} PROPERTIES FOLDER "sample")
endfunction(buildSampleAppChild)

function(buildSample SAMPLE_NAME)
    set(SAMPLE_FOLDER ${CMAKE_CURRENT_SOURCE_DIR}/${SAMPLE_NAME})
    set(COMMON_FOLDER ${CMAKE_CURRENT_SOURCE_DIR}/common)
    message(STATUS "Generating project file for example in ${SAMPLE_FOLDER}")

    project(${SAMPLE_NAME})
    file(GLOB HEADER_LIST ${SAMPLE_NAME}/*.h ${SAMPLE_NAME}/*.hpp)
    file(GLOB SOURCE_LIST ${SAMPLE_NAME}/*.cpp ${SAMPLE_NAME}/*.mm)
    source_group("sources" FILES ${HEADER_LIST} ${SOURCE_LIST})

    set(SHADER_DIR ${CMAKE_CURRENT_SOURCE_DIR}/${SAMPLE_NAME})
    file(GLOB SHADERS "${SHADER_DIR}/*.vert" "${SHADER_DIR}/*.frag" "${SHADER_DIR}/*.comp" "${SHADER_DIR}/*.geom" "${SHADER_DIR}/*.tesc" "${SHADER_DIR}/*.tese" "${SHADER_DIR}/*.mesh")
    source_group("shaders" FILES ${SHADERS})

    add_executable(${SAMPLE_NAME} ${HEADER_LIST} ${SOURCE_LIST} ${SHADERS})

    target_compile_definitions(${SAMPLE_NAME} PRIVATE -DEL_DEFINE_SAMPLE_PATH=\"${SAMPLE_FOLDER}/\")
    target_compile_definitions(${SAMPLE_NAME} PRIVATE -DEL_DEFINE_RESOURCE_PATH=\"${CMAKE_SOURCE_DIR}/resource/\")

    target_include_directories(${SAMPLE_NAME} PRIVATE "${COMMON_FOLDER}")

    target_link_libraries(${SAMPLE_NAME} PRIVATE "lib3d")
    target_link_libraries(${SAMPLE_NAME} PRIVATE "libplatform")
    target_link_libraries(${SAMPLE_NAME} PRIVATE "objparser")
    target_link_libraries(${SAMPLE_NAME} PRIVATE "meshoptimizer")
    target_link_libraries(${SAMPLE_NAME} PRIVATE "glfw")
    # target_link_libraries(${SAMPLE_NAME} PRIVATE "mtlpp")
    if(APPLE)
        target_link_libraries(${SAMPLE_NAME} PRIVATE "-framework AppKit")
    endif()
    if(BUILD_METAL)
        target_link_libraries(${SAMPLE_NAME} PRIVATE "-framework Metal")
        target_link_libraries(${SAMPLE_NAME} PRIVATE "-framework MetalKit")
    endif()
    # target_include_directories(${SAMPLE_NAME} PRIVATE "${mtlpp_SOURCE_DIR}")
    set_target_properties(${SAMPLE_NAME} PROPERTIES VS_DEBUGGER_WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}/${SAMPLE_NAME})
    set_target_properties(${SAMPLE_NAME} PROPERTIES PROJECT_LABEL ${SAMPLE_NAME})
    set_target_properties(${SAMPLE_NAME} PROPERTIES FOLDER "sample")
endfunction(buildSample)

buildSample("uniform")
buildSample("sharing")
buildSample("image")
buildSample("bunny")
if(WIN32)
    buildSample("window")
    buildSample("context")
    buildSample("multicontext")
    buildSample("multithread")
    buildSample("render-thread")
endif()
add_subdirectory(sampleapp)
if(APPLE)
    buildSample("osx-window")
endif()
buildSampleAppChild(bunny2)
if(APPLE)
    buildSample("driver")
    buildSampleAppChild(triangle)
    buildSampleAppChild(bunny-osx2)
    buildSampleAppChild(cow)
endif()
add_subdirectory(llvm-casting)
